plugins {
	id 'idea'
	id 'java'
	id 'checkstyle'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

allprojects {

	apply plugin: 'idea'
	apply plugin: 'java'
	apply plugin: 'checkstyle'

	group 'com.steatoda.nar'
	version '1.0.0'

	repositories {
		mavenCentral()
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(11)
		}
	}

	idea {
		module {
			jdkName = '11'
		}
	}

}

subprojects { subproject ->

	if (false
		|| subproject.name == 'nar-core'
		|| subproject.name == 'nar-jackson'
		|| subproject.name == 'nar-jooq'
	) {

		apply plugin: 'java-library'
		apply plugin: 'maven-publish'
		apply plugin: 'signing'

		java {
			withJavadocJar()
			withSourcesJar()
		}

		jar {
			manifest {
				attributes (
					'Implementation-Title': subproject.name,
					'Implementation-Version': subproject.version
				)
			}
		}

		artifacts {
			archives jar
		}

		publishing {
			publications {
				mavenJava(MavenPublication) {
					from components.java
					pom {
						url = 'https://github.com/gkresic/nar'
						licenses {
							license {
								name = 'The MIT License'
								url = 'https://github.com/gkresic/nar/blob/master/LICENSE'
							}
						}
						developers {
							developer {
								id = 'gkresic'
								name = 'Gordan Krešić'
							}
						}
						scm {
							url = 'https://github.com/gkresic/nar'
						}
					}
				}
			}
		}

		signing {
			sign publishing.publications.mavenJava
		}

	}

}

nexusPublishing {
	repositories {
		sonatype {
			nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
			snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
		}
	}
}
